buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        supportLibVersion = "35.0.0"
        kotlinVersion = "2.0.21"
        kotlin_version = kotlinVersion
        RNNKotlinVersion = kotlinVersion
        firebaseVersion = "24.1.0"
        ndkVersion = "27.1.12297006"
    }
    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath('com.google.gms:google-services:4.4.2')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        maven {
            url "$rootDir/../detox/node_modules/detox/Detox-android"
        }
        maven { url 'https://jitpack.io' }
    }
}

subprojects { subproject ->
    afterEvaluate {
        if (subproject.hasProperty("android")) {
            subproject.android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildFeatures {
                    buildConfig = true
                }
            }
        }
    }

    // Fix namespace for libraries with package in AndroidManifest.xml
    subproject.plugins.withId("com.android.library") {
        def manifestFile = subproject.file("src/main/AndroidManifest.xml")
        if (manifestFile.exists()) {
            def manifest = new XmlSlurper().parse(manifestFile)
            def packageName = manifest.@package.toString()
            if (packageName && !packageName.isEmpty()) {
                subproject.android {
                    namespace packageName
                }
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"

dependencyLocking {
    lockAllConfigurations()
}
